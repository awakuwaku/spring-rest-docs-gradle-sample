buildscript {
  ext {
    springBootVersion = '2.1.1.RELEASE'
    jdkVersion = 11
  }
  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.1")
  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

repositories {
  mavenCentral()
}

def projectName = 'spring-rest-docs-gradle-sample'
def projectVersion = '0.0.1-SNAPSHOT'

jar {
  baseName = projectName
  version = projectVersion
}

sourceCompatibility = jdkVersion
targetCompatibility = jdkVersion

//////////////////
// dependencies
//////////////////
dependencies {
  // Util
  implementation('org.projectlombok:lombok')
  annotationProcessor('org.projectlombok:lombok')
  implementation('com.jayway.jsonpath:json-path:2.2.0')
  // Spring Boot
  implementation('org.springframework.boot:spring-boot-starter-web')
  annotationProcessor('org.springframework.boot:spring-boot-configuration-processor')
  implementation('org.springframework.boot:spring-boot-starter-aop')
  // Spring Boot Test
  testImplementation('org.springframework.boot:spring-boot-starter-test')
  testImplementation('org.springframework.security:spring-security-test')
  // Spring REST Docs
  testImplementation('org.springframework.restdocs:spring-restdocs-core:2.0.2.RELEASE')
  testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.2.RELEASE')
}

compileJava.dependsOn(processResources)

ext {
  snippetsDir = file('build/generated-snippets')
}

asciidoctor {
  sourceDir 'src/main/asciidoc'
  attributes \
    'snippets': snippetsDir
}

///////////////////////
// System properties
///////////////////////
bootRun {
  systemProperties System.properties
}

///////////////////
// JavaDoc
///////////////////
javadoc {
  options.charSet = 'UTF-8'
  options.encoding = 'UTF-8'
}

//////////////////////////////////////
// JUnit Test Target
//////////////////////////////////////
test {
  exclude '**/*$*'
}

//////////////////////////////////////
// Gradle Wrapper Target
//////////////////////////////////////
wrapper {
  gradleVersion = '5.0'
}

